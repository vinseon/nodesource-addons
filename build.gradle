apply plugin: 'java'


archivesBaseName = 'nodesource-addons'


buildscript {
    repositories {
        if (project.hasProperty('local')) mavenLocal()

        jcenter()

        maven { url 'http://repository.activeeon.com/content/groups/proactive/' }
        maven { url "http://nexus.qmino.com/content/repositories/miredot" }
    }

    dependencies {
        classpath 'com.qmino:miredot-plugin:1.6.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}


allprojects {
    apply plugin: 'maven'
    
    group = 'org.ow2.proactive'
    version = nodesourceAddonsVersion


    rootProject.buildscript.repositories.each {
        repositories.add(it)
    }



    uploadArchives {
        repositories {
            mavenDeployer {
                snapshotRepository(url: "http://repository.activeeon.com/content/repositories/snapshots/") {
                    authentication(userName: "${System.getProperty('nexusUsername')}",
                            password: "${System.getProperty('nexusPassword')}")
                }
                repository(url: "http://repository.activeeon.com/content/repositories/releases/") {
                    authentication(userName: "${System.getProperty('nexusUsername')}",
                            password: "${System.getProperty('nexusPassword')}")
                }
            }
        }
    }
}

ext.javaSubprojects = subprojects.findAll { new File(it.projectDir, "src").exists() }

configure(javaSubprojects) {
    apply plugin: 'java'
    apply plugin: "sonar-runner"
    apply plugin: 'jacoco'  
    
    sonarRunner {
	    sonarProperties {    
	    	property "sonar.projectKey", "$project.group:nodesource-addons"
	    	property "sonar.projectName", "nodesource-addons" 
	        property "sonar.jdbc.driverClassName", "org.postgresql.Driver"
	        property "sonar.language", "java"
	        property "sonar.sources", "src/main"
	        property 'sonar.jacoco.reportPath', "${buildDir}/jacoco/testDebug.exec"
	        property 'sonar.junit.reportsPath', "${buildDir}/test-results"
	    }
	}
	    
    
	jacoco {
	    toolVersion = '0.7.1.201405082137'
	}
	
	jacocoTestReport {
	    additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
	    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
	    classDirectories =  files(sourceSets.main.output)
	    reports {
	        html.enabled = true
	        xml.enabled = true
	        csv.enabled = false
	    }
	}
	
        

    dependencies {
        compile 'log4j:log4j:1.2.17'
        compile 'com.google.guava:guava:19.0'
        
        compile "org.ow2.proactive:rm-server:${rmVersion}"
    	compile "org.objectweb.proactive:programming-core:${programmingVersion}"
    	compile 'com.google.guava:guava:19.0'
    	compile 'log4j:log4j:1.2.17'
    	
    	testCompile files("${System.properties['java.home']}/../lib/tools.jar")
        testCompile 'junit:junit:4.12'
    	testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    	testCompile 'org.mockito:mockito-core:1.10.19'
    }
    
    configurations {
	    shadowJar
	
	    runtime.extendsFrom = [compile]
	    runtime.exclude module: 'rm-server'
		runtime.exclude module: 'programming-core'
		runtime.exclude module: 'guava'
		runtime.exclude module: 'log4j'
		
	    testRuntime.extendsFrom = [testCompile]
	}

    
    jar {
        manifest {
            attributes("Implementation-Title": "ProActive",
                    "Implementation-Version": version,
                    "Specification-Version": version,
                    "Implementation-Vendor": "Activeeon - OASIS - INRIA Sophia Antipolis",
                    "Implementation-URL": "http://proactive.inria.fr"
            )
        }
    }
}



